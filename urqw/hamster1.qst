:10
pln Похождения отважного хомяка Семена: Побег
btn introduction, Начать игру
btn autors, Автор(ы)
btn instructions, Инструкции
end

:autors
pln Идея____________Triangle
pln Программист_____Triangle
pln Сценарий________Triangle
pln Наборщик________Triangle
pln В главной роли__Ныне покойный хомяк Семен
pln
pln Пишите мне на triangle@zmail.ru


btn 10, Назад
end

:instructions
pln Тут инструкции не нужны! И так все понятно!
btn 10, Назад
end

:introduction
pln Вы - мирно спящий сирийский хомяк по имени Семен. Вас, пока Вы спали, коварно заточили в клетку. Пока все.
btn beginning, Проснуться
perkill
invkill
end

:beginning
if nachalo=0 then pln Вы просыпаетесь и еще долго не можете понять, где находитесь. Скоро Вы осознаете, что это - хомячья клетка и замышляете побег. Но сначала нужно осмотреться.
pln Вы сидите в пустом домике. Слева от него лежит кормушка, сделанная из крышки кофе Нескафе, доверху наполненная жратвой. Справа поилка с молоком, а впереди шикарный тренажер (беговое колесо, о котором мечтает каждый заточенный в клетку хомяк).  Повсюду валяются бумажки (чтобы спать было мягче). При виде такой роскоши Вам почти расхотелось бежать, но Вы опомнились и приступаете к решительным действиям. К каким? Это еще надо обдумать.
if kot=5 then pln Кстати, рядом сидит злобный кот.
nachalo=1
btn koleso, Залезть в колесо
btn kormilka, Подойти к крышке
btn poilka, Подойти к поилке
btn escape, Сбежать
if kot=5 and Молоко then btn action, Прыснуть молоком в кота 
if kot=5 and Гвоздь then btn tknut, Ткнуть в кота гвоздем
if bumajka=0 then btn bumajka, Подобрать бумажку
end

:koleso
if kot=0 then pln Вы с трудом вскарабкались в колесо и начали отматывать километры, но тут (О УЖАС) на скрежет и визг несмазанного колеса прибежал кот. И пусть котяра снаружи и не может Вас достать, но Вам, хомяку, все равно как-то не по себе... Вы, конечно же, тормозите. Что делать теперь?
if kot=1 then pln Вы залезли в колесо и не обнаружили поблизости кота.
if kot=5 then pln Вы залезли в колесо. Кот прилежно дежурит рядом.
if kot=0 then kot=5
btn beginning, В домик
btn krutit, Крутить колесо
end

:krutit
IF kot=5 then pln В надежде убежать от кота в дальние дали Вы с бешеной скоростью  перебираете лапами в визжащем колесе. В полном согласии с законом природы "у кольца (aka у колеса) нет конца" Вы остаетесь на месте - да и кот тоже. Вы бежите, а кот смотрит... Кот смотрит, а вы бежите... Скоро Вы испускаете дух от переутомления. & btn introduction, Сначала
IF kot=1 then pln Вы залезли в колесо, покрутили его минуты две и стали просто суперским хомяком - сильным и всемогущим... После этого Вы ловко спрыгнули с колеса и уютно устроились в домике. & strong=1 & goto beginning
end

:poilka
pln Вы подползли к поилке. Там плещется вкусное на вид молоко.
btn drink, Вылакать молоко
if milk=0 then btn tmilk, Наполнить молоком щеку
btn beginning, Назад
end

:drink
pln Вы с наслаждением лакаете молоко, но вдруг обращаете внимание на странный привкус... К сожалению, уже слишком поздно.
btn introduction, Сначала
end

:tmilk
pln Вы заливаете молоко за щеку, и она медленно, но верно раздувается. & inv+ Молоко & milk=1
btn poilka, Назад
end

:action
pln Вы наводите прицел на врага (кота) и - БАТАРЕЯ, ОГОНЬ!!! ...то есть МОЛОКО!!! - брызжете на кота.  Промокший кот обиженно ушел зализывать шерсть.
kot=1
inv- Молоко
milk=0
btn beginning, В домик
end

:kormilka
pln Вы подошли к кормушке.
if jratva=0 then pln В ней валяется много хомячьего корму - горох, овес, семечки и другая жратва в этом роде.
if jratva=1 and gvozd=0 then pln Ничего себе! Вы замечаете, что на ее дне валяется гвоздик.
if jratva=0 then btn eat, Съесть корм
if jratva=1 and gvozd=0 then btn gvtake, Взять гвоздь
if jratva=1 and bumajka=1 and gvozd=0 then btn gvbum, Завернуть гвоздь в бумажку
if gvozd=1 then btn gvtake, Взять гвоздь в бумажке
btn beginning, Назад
end

:eat
pln Вы затратили полчаса на то, чтобы осилить корм: его было не так мало, как казалось.
jratva=1
btn kormilka, Назад
end

:bumajka
pln Вы запихиваете бумажку за щеку.
bumajka=1
inv+ Бумажка
btn beginning, В домик
end

:gvbum
pln Вы заворачиваете гвоздь в бумажку.
inv- Бумажка
bumajka=0
gvozd=1
btn kormilka, Назад
end

:gvtake
if gvozd=0 then pln Вы запихиваете острый гвоздь за щеку, и он ее с легкостью протыкает. С кровавой раной Вам не до побега! & btn introduction, Сначала
if gvozd=1 then pln Вы заталкиваете гвоздь в бумажке за щеку.& gvozd=50 & inv+ Гвоздь & btn beginning, В домик
end

:escape
pln Сбежать, да? Не все так просто. Вот Вы подошли к заветной дверце.
btn opendoor, Открыть дверь
btn grizt, Прогрызть отверстие
btn beginning, В домик
if Гвоздь then btn richag, Использовать гвоздь как рычаг 
end

:opendoor
if strong=0 then pln Как бы Вы ни пыжились, дверцу открыть не смогли бы.
if strong=1 then pln Вы теперь сильны, но не можете открыть дверь голыми лапами. Нужен какой-нибудь инструмент вроде ломика, специально для хомяков.
btn escape, Назад
end

:grizt
pln Вы начали грызть металл и только сломали все зубы. ...Беж жубов не до побега!
btn introduction, Сначала
end

:richag
if strong=0 then pln Вы вставили гвоздь в дверцу и уже собирались его повернуть, но обнаружили, что слишком слабы для этого. & btn escape, Назад
if strong=1 then pln Вы вставили гвоздь в дверцу клетки и повернули его накачанными мускулистыми лапами. Дверца открыта! Вы подтягиваетесь на передних лапах и под барабанную дробь и звуки фанфар выбираетесь из клетки! Ура! Уррра!!! Теперь в самую пору запустить hamster2.qst!
end

:tknut
pln Вы взяли гвоздь и попытались убить им кота. Кот с проклятиями отпрыгнул, но затем снова вернулся.
btn beginning, Назад
end